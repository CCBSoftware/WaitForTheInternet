apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

version '0.2'
group 'com.ccbsoftware'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName version
    }

}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.0'
}

def getRepositoryUsername() {
    return rootProject.hasProperty("ossrhUsername")? rootProject.property("ossrhUsername"):""
}

def getRepositoryPassword() {
    return rootProject.hasProperty("ossrhPassword")? rootProject.property("ossrhPassword"):""
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            pom.project {
                name 'WaitForTheInternet'
                packaging 'aar'
                artifactId 'waitfortheinternet'
                description 'The library to handle lost of internet connection'
                url 'https://github.com/ouvigna/WaitForTheInternet'

                scm {
                    url 'https://github.com/ouvigna/WaitForTheInternet'
                    connection 'scm:git@github.com:ouvigna/WaitForTheInternet.git'
                    developerConnection 'scm:git@github.com:ouvigna/WaitForTheInternet.git'
                }

                licenses {
                    license {
                        name 'MIT license'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'ouvigna'
                        name 'César Pérez'
                    }
                }
            }
        }
    }
}
